---

layout: post
title: 一份 vim 配置文件
category: Vim

---

vim 可以通过更改配置文件调整功能，不同的人编辑习惯都不一样，所得到的配置文件也截然不同，因此每个人都有属于自己的 vim。通过更改配置文件，你可以更改键位、调整设置、更改外观，甚至可以将 vim 打造成一个 IDE（当然个人觉得需要用 IDE 的话，还不如去用一个真的 IDE）！调整自己的配置文件，能够使你的编辑效率进一步提高。

<!--description-->

windows 中，vim 配置文件位于安装目录下，即 $VIM/\_vimrc，比如我的配置文件位于 d:\Software\Vim\_vimrc 。

linux 中，配置文件位于 $HOME/.vimrc 。

以下是我的配置文件，供大家参考（在何姥爷给我的第一份配置上不断修改得来。感谢何姥爷带我入坑！！！）

    """""""""""""""""""""""""Vundle"""""""""""""""""""""""""
    set nocompatible              " 去除 VI 一致性，必须
    filetype off                  " 必须

    " 设置包括 vundle 和初始化相关的 runtime path
    set rtp+=D:/Software/Vim/vimfiles/bundle/Vundle.vim
    call vundle#begin('D:/Software/Vim/vimfiles/bundle')

    " 让 vundle 管理插件版本，必须
    Plugin 'VundleVim/Vundle.vim'

    " 请将安装插件的命令放在 vundle#begin 和 vundle#end 之间。

    Plugin 'scrooloose/nerdcommenter'
    Plugin 'Solarized'
    Plugin 'Markdown'
    Plugin 'jiangmiao/auto-pairs'
    Bundle 'tpope/vim-surround'
    Bundle 'hotoo/pangu.vim'

    " 你的所有插件需要在下面这行之前
    call vundle#end()            " 必须
    filetype plugin indent on    " 必须 加载 vim 自带和插件相应的语法和文件类型相关脚本
    " 忽视插件改变缩进，可以使用以下替代：
    "filetype plugin on
    "
    " 简要帮助文档
    " :PluginList       - 列出所有已配置的插件
    " :PluginInstall    - 安装插件，追加 `!` 用以更新或使用 :PluginUpdate
    " :PluginSearch foo - 搜索 foo ; 追加 `!` 清除本地缓存
    " :PluginClean      - 清除未使用插件，需要确认；追加 `!` 自动批准移除未使用插件
    "
    " 查阅 :h vundle 获取更多细节和 wiki 以及 FAQ
    " 将你自己的非插件片段放在这行之后
    """""""""""""""""""""""""Vundle"""""""""""""""""""""""""

    " leader 键
    let mapleader=","

    " 配色设置
    " colors koehler
    set t_Co=256
    set background=dark
    colorscheme solarized

    " 浅色高亮当前行、列
    set cursorline
    set cursorcolumn

    " 大小写设置
    set ignorecase
    set smartcase

    " 设置高亮
    set hlsearch
    " 当光标一段时间保持不动了，就禁用高亮
    autocmd cursorhold * set nohlsearch
    " 当输入查找命令时，再启用高亮
    noremap n :set hlsearch<cr>n
    noremap N :set hlsearch<cr>N
    noremap / :set hlsearch<cr>/
    noremap ? :set hlsearch<cr>?
    noremap * *:set hlsearch<cr>

    " 设置文件编码格式
    set encoding=utf-8
    set fileencodings=utf-8,chinese,latin-1,gbk,gb18030,gk2312
    if has("win32")
     set fileencoding=chinese
    else
     set fileencoding=utf-8
    endif
    " 解决菜单乱码
    source $VIMRUNTIME/delmenu.vim
    source $VIMRUNTIME/menu.vim
    " 解决 consle 提示信息输出乱码
    language messages zh_CN.utf-8

    " gui 设置
    set guifont=Consolas:h12:cANSI
    " 不显示菜单、工具栏、滚动条
    set guioptions-=TrLm
    " 取消边框
    set go=

    source $VIMRUNTIME/mswin.vim
    behave mswin

    " tab 键设置
    set expandtab
    set softtabstop=4
    set tabstop=4
    set shiftwidth=4

    " public: 不缩进
    set cino=g0

    " 不备份
    set nobackup

    " 指针显示
    set mouse=a

    " 显示光标位置、行号
    set ruler
    set number

    " 记录光标位置，下次开启文件时移动光标至此
    augroup resCur
      autocmd!
      autocmd BufReadPost * call setpos(".", getpos("'\""))
    augroup END

    " 光标和底部距离
    set scrolloff=10

    " 打开文件时切换到文件目录
    autocmd BufEnter * silent! lcd %:p:h

    " 设置默认进制为十进制
    set nrformats=

    " 设置 Ex 命令缓存区长度
    set history=1000

    " 取消<C-V>快捷键以使用 Visual Block
    unmap <C-V>
    iunmap <C-V>
    " 取消<C-A>快捷键以使用加减操作
    unmap <C-A>
    iunmap <C-A>

    " 在命令行窗口 使用<C-p>和<C-n>完全替代<Up>和<Down>
    cnoremap <C-P> <Up>
    cnoremap <C-N> <Down>

    " 将 f{char}的 back 操作映射到"\"上，","充当 leader
    noremap \ ,

    " pangu.vim 插件自动添加空格
    autocmd BufWritePre *.markdown,*.md,*.text,*.txt,*.wiki,*.cnx call PanGuSpacing()

    " 补全菜单设置
    set wildmode=list:full

    map <F2> : call Save() <CR>
    map <F5> : !gdb --quiet %< <CR>
    map <F7> : call Compile() <CR>
    map <F8> : !bash check.sh <CR>
    map <F10> : !%<.exe <.in <CR>
    map <F9> : call Run() <CR>
    map <F4> : !echo % <CR>

    syn keyword Type dint
    syn keyword Special PROC
    syn keyword Special lld
    filetype plugin indent on
    syntax enable

    "set makeprg=g++\ %\ -o\ %<\ -g\ -Wall\ -Wextra\ -DLAEKOV_LOCAL
    func Save()
        if &filetype=='javascript'
            set expandtab
        endif
        call Find_NOTE()
        exec "w"
    endfunc

    func Find_NOTE()
        " 标记 NOTE
        exec "match PmenuSbar /NOTE/"
    endfunc
    call Find_NOTE()

    func Compile()
        exec "w"
        if &filetype=='cpp'
            exec "!g++ % -o %< -std=c++0x -g -Wall -Wextra -DLAEKOV_LOCAL && size %<.exe"
        elseif &filetype=='c'
            exec "!gcc %  -o %< -g"
        elseif &filetype=='pascal'
            exec "!fpc % -g"
        elseif &filetype=='sh'
            exec "!chmod +x %"
        elseif &filetype=='java'
            exec "!javac %"
        elseif &filetype=='tex'
            exec "!xelatex %"
        endif
    endfunc

    func Run()
        if &filetype=='python'
            exec "!python %"
        elseif &filetype=='sh'
            exec "!bash %"
        elseif &filetype=='java'
            exec "!java Main"
        elseif &filetype=='html'
            exec "!open ./%"
        elseif &filetype=='tex'
            exec "!open %<.pdf"
        elseif &filetype=='javascript'
            exec "!node %"
        else
            exec "!%<.exe"
        endif
    endfunc

    autocmd TextChanged,TextChangedI <buffer> silent write

    " LaTex Suit
    " REQUIRED. This makes vim invoke Latex-Suite when you open a tex file.
    filetype plugin on

    " IMPORTANT: win32 users will need to have 'shellslash' set so that latex
    " can be called correctly.
    set shellslash

    " IMPORTANT: grep will sometimes skip displaying the file name if you
    " search in a singe file. This will confuse Latex-Suite. Set your grep
    " program to always generate a file-name.
    set grepprg=grep\ -nH\ $*

    " OPTIONAL: This enables automatic indentation as you type.
    filetype indent on

    " OPTIONAL: Starting with Vim 7, the filetype of empty .tex files defaults to
    " 'plaintex' instead of 'tex', which results in vim-latex not being loaded.
    " The following changes the default filetype back to 'tex':
    let g:tex_flavor='latex'

